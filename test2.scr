npc Sara (shape = 989, portrait = 5, location = (1048, 2190))
{
attribute mood(0 to 100) = 50;
topic base
	{
	state base0
		{
	when starting:
		state = base1;
	when asked "Name?":
		topic = ask_name;
	when asked "Job?":
		topic = ask_job;
		}
	state base1
		{
	when Sara.mood < 40:
		say "Oh, it's you again.  What do you want now?";
		state = base0;
	when Sara.mood >= 40 && Sara.mood < 70:
		say "I'm busy, so make it quick!";
		state = base0;
	when Sara.mood >= 70:
		say "Oh, hello!  Nice to see you again!";
		state = base0;
		}
	}
topic ask_name
	{
	state name0
		{
	when starting:
		say "My name is Sara.";
		state = already_asked;
		topic = previous;
		}
	state already_asked
		{
	when starting:
		say "How many times do I have to tell you?  It's Sara!!";
		attribute mood = Sara.mood - 10;
		topic = previous;
		}
	}
topic ask_job
	{
	state job0
		{
	when starting:
		say "I wander the streets of Trinsic...";
		state = job1;
		}
	state job1
		{
	when starting:
		say "You walked away when I was trying to tell you before.";
		state = job0;
		topic = previous;
	when asked "Job?":
		say "Would you let me finish?";
	when asked "Doing?":
		say "...looking for an honest man.";
		state = job2;
		}
	state job2
		{
	when starting:
		say "Didn't I already tell you I'm looking for an honest man?";
	when told "I am an honest man.":
		say "That's what they all say.";
		state = explain;
	when asked "What about an honest woman?":
		say "You're looking at her.";
		state = honest_woman;
		}
	state honest_woman
		{
	when starting:
		state = job0;
	when told "And a lovely sight you are.":
				-- Want to use different color for descrs.
		say "She struggles to hold back a smile.";
		attribute mood = Sara.mood + 20;
		state = explain;
	when asked "You?":
		say "Well, Lord British thinks so.";
		state = explain;
		}
	state explain
		{
	when starting:
		say "So you wish to talk about my quest some more?";
	when asked "Why are you doing this?":
		say "The virtue Truth seems to be disappearing from Brittania. With the Avatar gone, Lord British believes that I am the last truly honest person in the realm; and so he has assigned me this task.";
		topic = previous;	-- ++++++More to be done.
	when asked "How much does this job pay?":
		say "What a fool you are!";
		attribute mood = Sara.mood - 20;
		state = fool;
		topic = previous;
	when told "Never mind.":
		say "Then I shall not waste my time.";
		state = job0;
		topic = previous;
		}
	state fool
		{
	when starting:
		say "Have you any more foolish questions about my quest?";
	when told "Yes.":
		say "Then try to keep your brain in gear.";
		state = explain;
	when told "No.":
		say "Good.";
		state = job0;
		topic = previous;
		}
	}
}
npc Iolo
	(
	shape = 465,
	portrait = 1,
	location = (1047, 2201)
	)
	{
	attribute mood(0 to 100) = 50;
	topic ask_name
		{
		state name0
			{
		when starting:
			state = initial;
		when asked "Name?":
			state = name1;
			attribute mood = Iolo.mood - 10;
			say "Do you not remember me?  It's Iolo!  Where have you been all these years, Avatar?";
			}
		state name1
			{
		when starting:
			state = initial;
		when told "Playing 'Wing Commander'.":
			say "Ah, well, Lord British, I am sure, must appreciate the tax revenue such activity generates.";
			state = name0;
		when told "Playing 'The Curse of Monkey Island'.":
			say "Such activity is traitorous!  But considering the poor state of Brittania, I must understand and forgive such lack of virtue.";
			state = name0;
			}
		state initial
			{
		when Iolo.mood >= 40:
			say "Hello, old friend.";
			state = name0;
		when Iolo.mood < 40:
			say "You do remember me now, don't you?";
			state = name0;
			}
		}
	}

			